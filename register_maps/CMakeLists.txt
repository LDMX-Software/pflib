# Generate the register map headers for the different supported ROCs
#  "v2" rocs were manually written so we just copy them into the correct spot
#  "v3+" rocs have their headers generated by a python script
# The extra CMake nonsense is setup so that the header generation only happens
# if necessary (new build or timestamp of source files is newer than generated files).
set(reg_map_src "${PROJECT_SOURCE_DIR}/register_maps/sipm_rocv2.h")
set(reg_map_header "include/register_maps/sipm_rocv2.h")
add_custom_command(
  OUTPUT ${reg_map_header}
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND cp ${reg_map_src} ${CMAKE_CURRENT_BINARY_DIR}/${reg_map_header}
  DEPENDS ${reg_map_src}
  COMMENT "Copying legacy C++ parameter LUTs from ${reg_map_src}"
  VERBATIM
)
add_custom_target(sipm_rocv2 DEPENDS ${reg_map_header})
list(APPEND generate_headers sipm_rocv2)
foreach(roc "si_roc" "si_rocv3b" "sipm_rocv3" "sipm_rocv3b")
  set(reg_map_src "${PROJECT_SOURCE_DIR}/register_maps/${roc}_regmap.yaml")
  set(reg_map_header "include/register_maps/${roc}.h")
  add_custom_command(
    OUTPUT ${reg_map_header}
    COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
    COMMAND python3 ${PROJECT_SOURCE_DIR}/register_maps/byte-pair-to-page-reg.py
      ${reg_map_src} ${CMAKE_CURRENT_BINARY_DIR}/${reg_map_header} --no-intermediate-yaml
    DEPENDS ${reg_map_src} ${PROJECT_SOURCE_DIR}/register_maps/byte-pair-to-page-reg.py
    COMMENT "Generating C++ parameter LUTs from ${reg_map_src}"
    VERBATIM
  )
  add_custom_target(${roc} DEPENDS ${reg_map_header})
  list(APPEND generate_headers "${roc}")
endforeach()

add_custom_command(
  OUTPUT "include/register_maps/direct_access.h"
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND python3
  ${PROJECT_SOURCE_DIR}/register_maps/write-direct-access-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/si_roc_directaccess_regmap.yaml
  ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps/direct_access.h
  DEPENDS
  ${PROJECT_SOURCE_DIR}/register_maps/write-direct-access-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/si_roc_directaccess_regmap.yaml
  COMMENT "Generating C++ Direct Access LUT from register_maps/si_roc_directaccess_regmap.yaml"
  VERBATIM
)
add_custom_target(direct_access DEPENDS include/register_maps/direct_access.h)
# do NOT include the direct_access header in the list of register maps to be included in the unifying header
# direct access "compilation" is simpler and is done directly within the ROC class and not
# within the Compiler

add_custom_command(
  OUTPUT "include/register_maps/lpgbt.h"
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND python3
  ${PROJECT_SOURCE_DIR}/register_maps/lpgbt-to-code.py
  ${PROJECT_SOURCE_DIR}/register_maps/lpgbt_regmap.yaml
  ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps/lpgbt.h
  DEPENDS
  ${PROJECT_SOURCE_DIR}/register_maps/lpgbt-to-code.py
  ${PROJECT_SOURCE_DIR}/register_maps/lpgbt_regmap.yaml
  COMMENT "Generating C++ lpGBT register LUT from register_maps/lpgbt_regmap.yaml"
  VERBATIM
)
add_custom_target(lpgbt_regmap DEPENDS include/register_maps/lpgbt.h)

add_custom_command(
  OUTPUT "include/register_maps/econd_test.h"
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND python3
  ${PROJECT_SOURCE_DIR}/register_maps/econ-to-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/ECOND_test.yaml
  ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps/econd_test.h
  DEPENDS
  ${PROJECT_SOURCE_DIR}/register_maps/econ-to-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/ECOND_test.yaml
  COMMENT "Generating C++ ECOND test register LUT from register_maps/ECOND_test.yaml"
  VERBATIM
)
add_custom_target(econd_test DEPENDS include/register_maps/econd_test.h)

add_custom_command(
  OUTPUT "include/register_maps/econd.h"
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND python3
  ${PROJECT_SOURCE_DIR}/register_maps/econ-to-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/ECOND_I2C_params_regmap.yaml
  ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps/econd.h
  DEPENDS
  ${PROJECT_SOURCE_DIR}/register_maps/econ-to-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/ECOND_I2C_params_regmap.yaml
  COMMENT "Generating C++ ECOND register LUT from register_maps/ECOND_I2C_params_regmap.yaml"
  VERBATIM
)
add_custom_target(econd DEPENDS include/register_maps/econd.h)

add_custom_command(
  OUTPUT "include/register_maps/econt.h"
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND python3
  ${PROJECT_SOURCE_DIR}/register_maps/econ-to-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/ECONT_I2C_params_regmap.yaml
  ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps/econt.h
  DEPENDS
  ${PROJECT_SOURCE_DIR}/register_maps/econ-to-header.py
  ${PROJECT_SOURCE_DIR}/register_maps/ECONT_I2C_params_regmap.yaml
  COMMENT "Generating C++ ECONT register LUT from register_maps/ECONT_I2C_params_regmap.yaml"
  VERBATIM
)
add_custom_target(econt DEPENDS include/register_maps/econt.h)

list(APPEND generate_headers_econ econd_test)
list(APPEND generate_headers_econ econd)
list(APPEND generate_headers_econ econt)

add_custom_command(
  OUTPUT include/register_maps/register_maps.h register_maps.cxx
  COMMAND mkdir -p ${CMAKE_CURRENT_BINARY_DIR}/include/register_maps
  COMMAND python3 ${PROJECT_SOURCE_DIR}/register_maps/write-unifying-header.py
    --roc_types ${generate_headers}
    --econ_types ${generate_headers_econ}
    --output-header include/register_maps/register_maps.h
    --output-src register_maps.cxx
  DEPENDS
  ${generate_headers}
  ${generate_headers_econ}
  ${PROJECT_SOURCE_DIR}/register_maps/register_maps_types.h
  ${PROJECT_SOURCE_DIR}/register_maps/write-unifying-header.py
  COMMENT "Writing unifying header for parameter LUTs"
  VERBATIM
)
add_custom_target(register_maps_src
  DEPENDS include/register_maps/register_maps.h register_maps.cxx)
add_library(register_maps SHARED register_maps.cxx)
target_include_directories(register_maps PUBLIC 
  "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>"
  "$<INSTALL_INTERFACE:include>")
target_include_directories(register_maps PUBLIC 
  "$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>"
  "$<INSTALL_INTERFACE:include>")
